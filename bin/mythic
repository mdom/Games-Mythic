#!/usr/bin/perl

use strict;
use warnings;
use Games::Mythic;
use Term::ShellUI;

my $game = Games::Mythic->new();

my $term = Term::ShellUI->new(
    commands => {
        ask => {
            proc => sub {
                my $r = $game->ask(@_);
                print "Answer: $r->{answer}"
                  . ( $r->{event} ? " Event: $r->{event}" : '' )
                  . " Roll: $r->{roll}\n";
            },
            minargs => 1,
            maxargs => 2,
            args    => \&complete_ranks,
        },
        add => {
            minargs => 2,
            args    => [ sub { complete_string(@_,[qw(npc thread)]) } ],
            proc    => sub {
                my $what   = shift;
                my $thing  = join( ' ', @_ );
                my $method = "add_$what";
                $game->$method($thing);
                print "$thing added to list of ${what}s.\n";
            },
        }
    },
);

sub complete_ranks {
    my ( $self, $cmpl ) = @_;
    return [ grep { index( $_, $cmpl->{str} ) == 0 } $game->ranks ];
}

sub complete_string {
   my ( $self, $cmpl, $strings ) = @_;
    return [ grep { index( $_, $cmpl->{str} ) == 0 } @$strings ];
}

$term->run();
